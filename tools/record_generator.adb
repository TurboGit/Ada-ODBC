------------------------------------------------------------------------------
--                             Record_Generator                             --
--                                                                          --
--                         Copyright (C) 2010-2018                          --
--                                                                          --
--  This library is free software; you can redistribute it and/or modify    --
--  it under the terms of the GNU General Public License as published by    --
--  the Free Software Foundation; either version 2 of the License, or (at   --
--  your option) any later version.                                         --
--                                                                          --
--  This library is distributed in the hope that it will be useful, but     --
--  WITHOUT ANY WARRANTY; without even the implied warranty of              --
--  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU       --
--  General Public License for more details.                                --
--                                                                          --
--  You should have received a copy of the GNU General Public License       --
--  along with this library; if not, write to the Free Software Foundation, --
--  Inc., 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.       --
------------------------------------------------------------------------------
--
--  Author : Gautier de Montmollin
--
--  Small utility to generate a record type corresponding
--  to a database table's model.
--
--  Usage: record_generator ODBC_Source Table_Name

with Ada.Command_Line;
with Ada.Text_IO;
with Ada.Strings.Fixed;
with Ada.Exceptions;

with Databases.Select_Query;

procedure Record_Generator is

   use Ada;

   package SQL renames Databases.Select_Query;

   Clients : Databases.Database;
   Query   : SQL.Select_Data;

   --------------
   -- Generate --
   --------------

   procedure Generate is

      use Ada.Command_Line;
      use Ada.Strings.Fixed;
      use Ada.Exceptions;

      Driver : constant String := Argument (1);
      Table  : constant String := Argument (2);

      -----------
      -- Blurb --
      -----------

      function Blurb return String is
      begin
         return
           "-- Generated by record_generator, for data source '"
           & Driver & "' and table '" & Table & "'.";
      end;

      ------------
      -- Adaize --
      ------------

      function Adaize (S : String) return String is
         T    : String := S;
         J    : Integer;
         Last : Character := 'x';
      begin
         J := T'First;

         for I in S'Range loop
            case S (I) is
               when ' ' =>
                  T (J) := '_';
                  if Last = '_' then
                     J := J - 1; -- avoid double underscores
                  end if;

               when '(' | ')' | '-' =>
                  J := J - 1;

               when others =>
                  T (J) := S (I);
            end case;

            Last := T (J);
            J := J + 1;
         end loop;

         return T (T'First .. J - 1);
      end Adaize;

      Found : Boolean;
      Max   : Natural := 0;

   begin
      Databases.Connect (Clients, Driver, "", "");
      SQL.Execute (Clients, "select * from " & Table, Query);

      SQL.Fetch (Query, Found);

      if Found then
         Text_IO.Put_Line (Blurb);
         Text_IO.Put_Line ("type " & Table & " is record");

         for Column in 1 .. SQL.Number_Of_Columns (Query) loop
            Max := Natural'Max
              (Max, Adaize (SQL.Get_Name (Query, Column))'Length);
         end loop;

         for Column in 1 .. SQL.Number_Of_Columns (Query) loop
            Text_IO.Put_Line
              ("   "
               & Adaize (SQL.Get_Name (Query, Column))
               & (Max - Adaize (SQL.Get_Name (Query, Column))'Length) * ' '
               & ": "
               & SQL.Get_Model_Name (Query, Column) & "; -- "
               & Integer'Image (Column));
         end loop;

         Text_IO.Put_Line ("end record; -- " & Table);
      end if;

      Databases.Close (Clients);
   exception
      when E : others =>
         Text_IO.Put_Line
           (Text_IO.Standard_Error,
            "Problem with ODBC :-( ... : " & Exception_Message (E));
         Databases.Close (Clients);
   end Generate;

begin
   Generate;
exception
   when Constraint_Error =>
      Text_IO.Put_Line
        (Text_IO.Standard_Error,
         "Usage: Record_generator ODBC_Source Table_Name");
end Record_Generator;
